/COM
/COM    Datei: QPIEZO.F18
/COM

/PREP7
/TITLE, Quarz-Doppelstimmgabel, 3D anisotrop

/COM
/COM   Parameter
/COM
elgr=0.25          * Elementgroesse [mm]
elty=98            * Elementtyp: STIF5 bzw. 98
thik=.100          * Materialdicke [mm]

/COM
/COM   Geometrieparameter 
/COM   alle Masse in [mm]
/COM
l1=1.2             * Laenge Befestigung
l2=2.5             * halbe Stimmgabellaenge
b1=.22             * Breite Befestigung
wid=.2             * Stimmgabelbreite
abst=.15           * Stimmgabelabstand

/COM
/COM   Hilfsvariablen
/COM
len=(l1+l2)
bwi=(b1+wid)
bwa=(bwi+abst)
bwaw=(bwa+wid)
bges=(bwaw+b1)
lges=(2*len)

/COM
/COM   Materialdaten
/COM
et,3,elty          * Piezoelement
/com   nlsize,300
mp,dens,3,2.648-9     * Dichte von Quarz [kg/mm3]
/COM
/COM     CE-Matrix: Elastizitaetsmodul-Matrix
/COM     T=25 Grad-C, in [N/mm2], adiabatisch
/COM     (aus: Tichy/Gautschi)
/COM
nl,3,271,86.8e3,7.04e3,11.91e3,-18.04e3
nl,3,277,86.8e3,11.91e3,18.04e3
nl,3,282,105.75e3
nl,3,286,58.2e3
nl,3,289,58.2e3,-18.04e3
nl,3,291,39.88e3

/COM
/COM     E-Matrix: Piezoelektrische-Modulmatrix
/COM     T=20 Grad-C, in [C/mm2]
/COM
nl,3,133,.171e-6,-.171e-6,0,-.041e-6
nl,3,143,.041e-6,-.171e-6

/COM
/COM     EPS-Matrix: Dielektrizitaetsmatrix (Re)
/COM     eps0 = 8.854e-18 [C/Vmm]
/COM
nl,3,103,39.967e-18
nl,3,157,39.967e-18
nl,3,211,41.029e-18

/COM     EPS-Matrix: Dielektrizitaetsmatrix (Im)
/COM
/COM     Geometrie erstellen: Z-Schnitt
/COM
/COM     Knotenpunkte generieren
/COM
K,1
k,2,l1
K,3,l1,b1
K,4,len,b1
K,5,len,bwi
K,6,l1,bwi
K,7,l1,bwa
K,8,len,bwa
K,9,len,bwaw
K,10,l1,bwaw
K,11,l1,bges
K,12,0,bges

/COM
/COM     Linien generieren
/COM
L,1,2
RP11,1,1
L,12,1

/COM
/COM     Flaechen generieren
/COM
AL,ALL

/COM
/COM     Zweite Flaeche definieren
/COM
AGEN,2,ALL,,,,,thik

/COM
/COM     vertikale Linien ziehen
/COM
L,1,13
RP11,1,1
L,12,24

/COM
/COM     Randflaechen definieren
/COM
A,1,13,24,12
A,1,2,14,13
RP11,1,1,1,1

/COM
/COM     Volumen aus Einzelflaechen
/COM
VA,ALL
TYPE,3
MAT,3
ELSI,elgr
VMESH,1  * Struktur vernetzen

/COM
/COM     2. Haelfte generieren
/COM
LOCAL,11,,len
VSYM,1,ALL
csys
NUMMRG,NODE    * Knoten verbinden

/COM
/COM     Festhalte-Randbedingungen
/COM     beide Enden fixieren
nsel,x
nasel,x,lges
d,all,ux
d,all,uy
d,all,uz

/COM
/COM     alle anderen Freiheitsgrade festsetzen
/COM
nall    
d,all,temp
d,all,mag
wsort,x     * wavefront erniedrigen

/COM
/COM     elektrische Festhaltebedingungen
/COM
/COM     Kurzschluss: E=0
/COM
nsel,z
nt,all,volt,0
nsel,z,thik
nt,all,volt,0
lwrit    * Schreibe Lastfall 1

/COM
/COM     Offene Elektroden: D=0
/COM
nsel,z,thik
ntdel,all,volt
nall
lwrit    * Schreibe Lastfall 2
cnvr,,,,,,,-1   * Vermeide Abbruch infolge negativer CE-Matrix
/view,1,1,1,1

/COM
/COM      M D O F s  selektieren...!!!
/COM
total,500
kan,2
kay,2,20
kay,3,20
