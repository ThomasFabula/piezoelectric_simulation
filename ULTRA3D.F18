/COM ******************************************************************
/COM *                       F18-FILE                                 *
/COM *                                                                *
/COM * TITEL: ULTRASCHALLWANDLER - axialsymmetrisch                  *
/COM ******************************************************************
/COM *                                                                *
/COM * FUNKTION:                                                      *
/COM *                                                                *
/COM * 3D-Modell einer radialsymmetrischen Ultraschallwandler-Geome-  *
/COM * trie mit Anpassschicht, das in Form eines "Kuchenstuecks"      *
/COM * mit entsprechenden Symmetrierandbedingungen erstellt wurde.    *
/COM * Der Winkelbereich "phi" des Tortenstuecks ist parametrisiert   *
/COM * und damit frei waehlbar. Um eine dreidimensionale Vernetzung   *
/COM * sicherzustellen, wird ausgehend von der Zylinderachse ein      *
/COM * moeglichst kleiner Innenradius "rmin" definiert.               *
/COM *                                                                *
/COM *                                                                *
/COM * EINGABE-PARAMETER:                                             *
/COM *                                                                *
/COM * Gegliedert nach  - Analysetyp mit Optionen                     *
/COM *                  - Graphische Optionen                         *
/COM *                  - Goemetrie                                   *
/COM *                  - Linienteilung                               *
/COM *                  - Materialwerte                               *
/COM *                                                                *
/COM * VERWENDETE MAKROS:    keine                                    *
/COM *                                                                *
/COM *                                                                *
/COM * BEMERKUNGEN:                                                   *
/COM *                                                                *
/COM *                                                                *
/COM * AUTOR(EN): N. Hey, Th. Fabula                                  *
/COM *                                                                *
/COM * EINGABEDATUM/LETZTE AENDERUNG: 14.10.1992                      *
/COM *                                                                *
/COM ******************************************************************

/SHOW,X11
/com  Materialdaten einlesen aus: ULTRA.MAT
/com  Werte sind definiert bzgl. kartesischen Koordinaten

/COM
/COM ==============================
/COM    Datei: U L T R A 3 D. F 1 8
/COM ==============================
/COM
/TITLE, Ultraschallwandler - axialsymmetrisch
/COM
/COM -------------- ANALYSETYP MIT OPTIONEN definieren
/COM
/COM siehe Dateien:
/COM - ULTRA.KAN0 : statische Analyse
/COM - ULTRA.KAN2 : Modalanalyse
/COM - ULTRA.KAN6 : transiente Analyse
/COM diese Dateien sind am zum Schluss einzulesen.
/COM
/COM -------------- GRAPHISCHE OPTIONEN waehlen
/COM
/pbc,all,2
/PNUM,KPOI,1
/PNUM,LINE,1
/PNUM,VOLU,1
/PNUM,MAT,1
/VUP,,z
/VIEW,,-1,-1,1
/TYPE,,3
/COM
/COM ------------- GEOMETRISCHE EINGABEPARAMETER (Einheiten in m)
rmin=0.01E-3             ! minimaler Innenradius zwecks Vernetzung
r1=25E-3                 ! Radius der Piezokeramik
r2=r1+10E-3              ! Radius der Silikonstruktur
r3=r2+2E-3               ! Aussenradius SOLEF
z1=2E-3                  ! Dicke von SOLEF
z2=z1+4.3E-3             ! + Silikon
z3=z2+2E-3               ! + Silikon-Aussenbereich
z4=z2+12.1E-3            ! + Piezokeramik
z5=50E-3                 ! Hoehe der SOLEF-Aussenwandung
phi=5                    ! Winkelbereich des "Tortenstuecks"
                         ! in Winkelgraden
ensp=25e-3               ! Einspannung Aussenwandung
/COM
/COM ----------------------- Parameter zur Linienteilung
rat1=10                  ! radiale Linienteilung von SOLEF
rat2=3                   ! rad. Linient. Silikon-Aussenbereich
rat3=2                   ! rad. Linient. SOLEF-Aussenwandung
zt1=4                    ! SOLEF-Dickenteilung (Zylindergrundfl.)
zt2=4                    ! Dickenteilung von Silikon
zt3=2                    ! Dickenteilung Silikon-Aussenbereich
zt4=8                    ! Dickenteilung der Piezokeramik
zt5=6                    ! Hoehenteilung der SOLEF-Aussenwandung
phit=4                   ! Teilung ueber Winkelbereich
/COM
/COM ----------------------- Materialeigenschaften definieren
/COM
/COM ----------------------- Knotenpunkte und Linien generieren

local,11,1               ! lokales zylindr. Koordinatensystem
k,1,rmin                 ! Keypoint fuer minimal. Innenrad.
k,2,r1
k,3,r2
k,4,r3
/COM
l,1,2                    ! Linien der SOLEF-Grundflaeche...
l,2,3
l,3,4
ldvs,1,,rat1             ! Teilung der erzeugten Linien...
ldvs,2,,rat2
ldvs,3,,rat3
lgen,2,1,3,1,,,z1,10     ! Parallele Linienzuege generieren...
lgen,2,1,3,1,,,z2,20
lgen,2,1,3,1,,,z3,30
lgen,2,1,,,,,z4,40
lgen,2,3,,,,,z5,50
l,1,11                   ! Verbindungslinien generieren
RP4,1,1                  ! und jeweils in z-Richtung
ldvs,all,,zt1            ! unterteilen...
l,11,21
RP4,1,1
ldvs,all,,zt2
l,21,31
RP4,1,1
ldvs,all,,zt3
l,31,41
RP2,1,1
ldvs,all,,zt4
l,33,53
l,34,54
ldvs,all,,zt5
/COM
/COM -------------------- Flaechenquerschnitt generieren
A,1,2,12,11         ! Flaechennummerierung von links nach
A,2,3,13,12         ! rechts und in z-Richtung steigend...
A,3,4,14,13
A,11,12,22,21
A,12,13,23,22
A,13,14,24,23
A,21,22,32,31
A,22,23,33,32
A,23,24,34,33
A,31,32,42,41
A,33,34,54,53
/COM ---------------------------- VOLUMEN generieren
k,100,r3,phi,z5          ! Draglinie definieren...
l,54,100
VDRAG,1,2,3,4,5,6,31
VDRAG,7,8,9,10,11,,31
ldvs,all,,phit
NUMM,all,1.0E-7

ET,1,45
ET,3,5

/com  Gummiartiges Materialverhalten nach Mooney-Rivlin Theorie
ET,2,86             ! hyperelastisches Element
con1=2.366e5        ! = E-Modul/6 von SILIKON
con2=0
mp,ex,1,1.0         ! Dummy E-Modul
mp,nuxy,1,0.4998    ! Poisson-Zahl
mp,dens,1,1200      ! Dichte
nl,1,1,0,10         ! Temperaturbereich
nl,1,7,con1,con1    ! Mooney Koeffizient A
nl,1,13,con2,con2   ! Mooney Koeffizient B
kay,6,1             ! grosse Auslenkungen (notwendige Option)

/COM -------------------------- Vernetzen der Volumina
csys,0

MAT,2             ! SOLEF-Ummantelung vernetzen...
TYPE,1
VMESH,1,3
VMESH,6,9,3
VMESH,11
/COM

MAT,1             ! Silikon-Struktur vernetzen...
! TYPE,1            ! Elementtyp: STIF 45
TYPE,2            ! Elementtyp: STIF 86
VMESH,4,5
VMESH,8
/COM

MAT,3             ! Piezokeramik vernetzen...
TYPE,3
VMESH,7,10,3

/COM -------------------------- RANDBEDINGUNGEN definieren
                  ! Einspannungen definieren
local,11,1
arsel,area,52
narea,1
nrsel,z,z5-ensp,z5     ! SOLEF-Zylinderaussenflaeche auf
                       ! Laenge z5-ensp allseitig einspannen ...
d,all,ux
d,all,uy
d,all,uz
arall
nall
/com                     ! Symmetriebedingungen definieren
SYMBC,11,1,rmin          ! Symmetrie bzgl. Radius
SYMBC,11,2,0             ! Symmetrie bezgl. Randflaechen
SYMBC,11,2,phi           ! des Winkelbereichs
CSYS
WSORT

/COM  Analyseoption und elektrische Randbedingungen
/COM  aus entsprechenden Dateien einlesen.
